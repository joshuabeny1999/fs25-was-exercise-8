// acting agent
role_goal(R, G) :-
    role_mission(R, _, M) & mission_goal(M, G).

can_achieve(G) :-
    .relevant_plans({+!G[scheme(_)]}, LP) & LP \== [].

i_have_plans_for(R) :-
    not (role_goal(R, G) & not can_achieve(G)).

// The agent has a belief about the location of the W3C Web of Thing (WoT) Thing Description (TD)
// that describes a Thing of type https://ci.mines-stetienne.fr/kg/ontology#PhantomX
robot_td("https://raw.githubusercontent.com/Interactions-HSG/example-tds/main/tds/leubot1.ttl").

/* Initial goals */
!start. // the agent has the goal to start

/* 
 * Plan for reacting to the addition of the goal !start
 * Triggering event: addition of goal !start
 * Context: the agent believes that it can manage a group and a scheme in an organization
 * Body: greets the user
*/
@start_plan
+!start : true <-
	.print("Hello world").

+org_workspace_available(WkspName, OrgArtName, GroupArtName, SchemeBoardName)[source(org_agent)] : true
  <- .print("Joining marketplace: ", WkspName, " " , OrgArtName, " ", GroupArtName);
     joinWorkspace(WkspName, WkspId);
     focusWhenAvailable(OrgArtName)[wid(WkspName)];
     focusWhenAvailable(GroupArtName)[wid(WkspName)];
	 focusWhenAvailable(SchemeBoardName)[wid(WkspName)];
	 .wait(1000); // wait for the workspace to be ready
	.

@adopt_role_after_invited_and_can_adopt
+available_role(Role, org_name(OrgName)) : i_have_plans_for(R) <-
    .print("I will adopt role ‘", Role, "’ – I have plans for it");
    adoptRole(Role).

@adopt_role_after_invited_and_cannot_adopt
+available_role(Role, org_name(OrgName)) : true <-
	.print("I can't adopt role ‘", Role, "’ – I have no plans for it");
	-available_role(Role, org_name(OrgName)).

/* 
 * Plan for reacting to the addition of the goal !manifest_temperature
 * Triggering event: addition of goal !manifest_temperature
 * Context: the agent believes that there is a temperature in Celsius and
 * that a WoT TD of an onto:PhantomX is located at Location
 * Body: converts the temperature from Celsius to binary degrees that are compatible with the 
 * movement of the robotic arm. Then, manifests the temperature with the robotic arm
*/
@manifest_temperature_plan 
+!manifest_temperature : temperature(Celsius) & robot_td(Location) <-
	.print("I will manifest the temperature: ", Celsius);
	makeArtifact("converter", "tools.Converter", [], ConverterId); // creates a converter artifact
	convert(Celsius, -20.00, 20.00, 200.00, 830.00, Degrees)[artifact_id(ConverterId)]; // converts Celsius to binary degress based on the input scale
	.print("Temperature Manifesting (moving robotic arm to): ", Degrees);

	/* 
	 * If you want to test with the real robotic arm, 
	 * follow the instructions here: https://github.com/HSG-WAS-FS25/exercise-8/blob/main/README.md#test-with-the-real-phantomx-reactor-robot-arm
	 */
	// creates a ThingArtifact based on the TD of the robotic arm
	makeArtifact("leubot1", "org.hyperagents.jacamo.artifacts.wot.ThingArtifact", [Location, true], Leubot1Id); 
	
	// sets the API key for controlling the robotic arm as an authenticated user
	//setAPIKey("9c1a645ec824562822a28d0cef4e4845")[artifact_id(Leubot1Id)];

	// invokes the action onto:SetWristAngle for manifesting the temperature with the wrist of the robotic arm
	invokeAction("https://ci.mines-stetienne.fr/kg/ontology#SetWristAngle", ["https://www.w3.org/2019/wot/json-schema#IntegerSchema"], [Degrees])[artifact_id(Leubot1Id)].

/* Import behavior of agents that work in CArtAgO environments */
{ include("$jacamoJar/templates/common-cartago.asl") }

/* Import behavior of agents that work in MOISE organizations */
{ include("$jacamoJar/templates/common-moise.asl") }

/* Import behavior of agents that reason on MOISE organizations */
{ include("$moiseJar/asl/org-rules.asl") }

/* Import behavior of agents that react to organizational events
(if observing, i.e. being focused on the appropriate organization artifacts) */
{ include("inc/skills.asl") }
